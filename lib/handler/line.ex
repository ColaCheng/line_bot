defmodule LineBot.Handler.Line do
  require Logger
  alias LineBot.Handler.Utils, as: HUtils
  alias LineBot.Context.LineMessage
  alias LineBot.Context.GoogleDailyTrends
  alias LineBot.Context.Pharmacies

  def init(req_in, opts) do
    request = %{
      method: :cowboy_req.method(req_in),
      data: %{}
    }

    {result, req_done} =
      case :cowboy_req.has_body(req_in) do
        true ->
          {:ok, body, req_out} = HUtils.read_body(req_in, <<>>)

          case HUtils.decode_body(body, :json) do
            {:ok, data} ->
              {process_request(Map.put(request, :data, data)), req_out}

            :error ->
              {:invalid_json, req_out}
          end

        false ->
          {process_request(request), req_in}
      end

    case make_response(result) do
      {code, response} ->
        {:ok,
         :cowboy_req.reply(
           code,
           %{<<"content-type">> => <<"application/json">>},
           :jiffy.encode(response),
           req_done
         ), opts}

      code ->
        {:ok, :cowboy_req.reply(code, req_done), opts}
    end
  end

  defp process_request(%{method: method, data: %{"events" => events}}) do
    case method do
      "POST" ->
        process_line_events(events)
        {:ok, %{message: "ok"}}

      _ ->
        :method_not_allowed
    end
  end

  defp process_request(_) do
    :invalid_line_event
  end

  defp process_line_events([]), do: :ok

  defp process_line_events([
         %{
           "type" => "message",
           "replyToken" => reply_token,
           "message" => %{"text" => input, "type" => "text"}
         }
         | tail
       ]) do
    parse_keyword(input)
    |> LineMessage.reply(reply_token)

    process_line_events(tail)
  end

  defp process_line_events([
         %{
           "type" => "message",
           "replyToken" => reply_token,
           "message" => %{"type" => "location", "latitude" => lat, "longitude" => long}
         }
         | tail
       ]) do
    for %{
          "geometry" => %{"coordinates" => [p_long, p_lat]},
          "properties" => %{
            "name" => name,
            "address" => address,
            "mask_adult" => mask_adult,
            "mask_child" => mask_child
          }
        } <- Pharmacies.find_near_pharmacies({lat, long}) do
      %{
        "type" => "location",
        "title" => name <> "\næˆï¼š #{mask_adult}ï¼Œå…’ï¼š#{mask_child}",
        "address" => address,
        "latitude" => p_lat,
        "longitude" => p_long
      }
    end
    |> LineMessage.reply(reply_token)

    process_line_events(tail)
  end

  defp process_line_events([event | tail]) do
    Logger.info("Unknown event: #{inspect(event)}")
    process_line_events(tail)
  end

  defp parse_keyword(input) when input in ["å°ç£", "è‡ºç£"] do
    trends = GoogleDailyTrends.get("TW")
    [%{"type" => "text", "text" => trends}]
  end

  defp parse_keyword("æ‰¾å£ç½©") do
    find_mask_action = %{
      "type" => "location",
      "label" => "GPSæœå°‹ï¼ˆè«‹é»žä¸­é–“åœ°å€ï¼‰"
    }

    news_action = %{
      "type" => "uri",
      "label" => "çœ‹çœ‹æœ€æ–°ç–«æƒ…æƒ…å ±",
      "uri" => "https://www.cdc.gov.tw/"
    }

    trends_action = %{
      "type" => "message",
      "label" => "å°ç£ä»Šå¤©éƒ½åœ¨æœä»€éº¼",
      "text" => "å°ç£"
    }

    [
      %{
        "type" => "template",
        "altText" => "æƒ³æ‰¾é™„è¿‘ç‰¹ç´„è—¥å±€è²·å£ç½©å—Žï¼Ÿ",
        "template" => %{
          "type" => "buttons",
          "title" => "æƒ³æ‰¾é™„è¿‘ç‰¹ç´„è—¥å±€è²·å£ç½©å—Žï¼Ÿ",
          "text" => "å‰å¾€è—¥å±€å‰éº»ç…©è«‹ç¢ºèªç‡Ÿæ¥­æ™‚é–“åŠè³¼è²·é ˆçŸ¥ï¼Œè¬è¬ðŸ™‚",
          "defaultAction" => find_mask_action,
          "actions" => [find_mask_action, news_action, trends_action]
        }
      }
    ]
  end

  defp parse_keyword(_input) do
    []
  end

  defp make_response({:ok, result}), do: {200, result}
  defp make_response(:invalid_json), do: {400, %{message: "Invalid JSON."}}
  defp make_response(:invalid_line_event), do: {400, %{message: "Invalid Line event."}}
  defp make_response(:method_not_allowed), do: {405, %{message: "Method not allowed."}}
end
